buildscript{
    repositories{
        jcenter()
        maven{
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies{
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

group = "chylex.javacheck"
ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') == null ? "CUSTOM" : System.getenv('BUILD_NUMBER')

version = project.hasProperty("mavendir") ? "v1.2-b"+project.buildnumber : "MC-UNIVERSAL  v1.2"
String archiveSuffix = (project.hasProperty("mavendir") ? "-" : "  ")+version+".jar"

minecraft{
    version = "1.8.8-11.15.0.1618-1.8.8"
    runDir = "run"
    mappings = "snapshot_20151122"
    makeObfSourceJar = false
}

task java7Jar(type: Jar, dependsOn: "jar"){
    from sourceSets.main.output
    
    manifest{
        attributes 'TweakClass': 'chylex.javacheck.Java7Checker'
    }
    
    archiveName = "Java7Checker"+archiveSuffix
}

task java8Jar(type: Jar, dependsOn: "jar"){
    from sourceSets.main.output
    
    manifest{
        attributes 'TweakClass': 'chylex.javacheck.Java8Checker'
    }
    
    archiveName = "Java8Checker"+archiveSuffix
}

task shadeJar(type: Jar, dependsOn: "jar"){
    from sourceSets.main.output
    
    archiveName = "JavaCheckerShade"+archiveSuffix
}

jar.enabled = false

reobf{
    reobf java7Jar{ classpath -> classpath = configurations.compile }
    reobf java8Jar{ classpath -> classpath = configurations.compile }
    reobf shadeJar{ classpath -> classpath = configurations.compile }
}

reobf.enabled = false

processResources{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs){
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    
    from(sourceSets.main.resources.srcDirs){
        exclude 'mcmod.info'
    }
}

publishing{
    tasks.publish.dependsOn 'build'
    
    publications{
        mavenJava(MavenPublication){
            artifact shadeJar
        }
    }
    
    repositories{
        if (project.hasProperty("mavendir")){
            maven { url mavendir }
        }
    }
}